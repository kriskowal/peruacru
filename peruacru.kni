! hills = 0
  jungle = 1
  beach = 2
  mountain = 3
  at = hills

  # brother and sister
  hand = 4

  # inventory
  bamboo = 0
  flower = 0
  freshwater.pumpkin = 0
  brine.pumpkin = 0
  mushroom = 0
  pumpkin = 0
  reed = 0
  soaked.reed = 0
  rock = 0
  rubber = 0
  sand.pumpkin = 0
  sap.pumpkin = 0
  shrinking.potion = 0
  growing.potion = 0
  vial = 0
  brine.vial = 0
  # TODO sap.vial (rubber band + growing potion -> rubber)
  freshwater.vial = 0
  hammer = 0
  paper = 0
  airplane = 0
  ballista = 0

  # world state
  chat = 0
  homestead = 0
  lion = 1
  cat = 0
  tap = 0
  placed.ballista = 0
  launch.pad = 0

There were once a brother and sister who would shrink themselves with red
potions and explore the world from the back of a giant paper airplane.

They discovered an uninhabited island amid the sparkling Pacific ocean and
named it Peru√°cru.

They alighted upon a green knoll and drank their blue growing potions, only to
damage their plane and lose their stock of potions.

The clever pair set out to explore the island and hopefully find a way home
with their four hands and two cunning heads.

+ <> <continue> [Continue. ]
->maybe.chat()
>

# bamboo / hand
# flower / hand
# pumpkin / sap.pumpkin / brine.pumpkin / freshwater.pumpkin / 2 hands
# mushroom / hand
# reed / hand

- @hills
  ! at = hills
  Hills. There is a tall, green knoll and a {~broad|wide} valley with a
  freshwater river. There are {~numerous|many|uncountable|plentiful}
  pumpkins.
  - {not pumpkin and hand<2}
    You would need to drop something to pick up a pumpkin.
    One of you would need two free hands.
  - {ballista and not placed.ballista}
    This looks like a good spot for a ballista.
  - {placed.ballista}
    Your ballista stands waiting for a launch vehicle large
    enough to carry two, tiny passengers.
  - {launch.pad} /
    {(shrinking.potion)|
      {(mushroom)
      | You might be too heavy for the plane to carry you both away.
      | {(mushroom)||This mushroom|These mushrooms}
        might be handy if we were to shrink ourselves and fly
        away on this plane, and an animal might well eat it straight,
        but we should prepare potions.
      }
    | You'}ve got one shrinking potion, but it'}s dangerous to go alone.
    | Your giant airplane stands on the giant slingshot.
      All systems are go. Awaiting a tiny crew for launch.
    }
  @hills.choice
  + <launch> <quaff> {-launch.pad} {+placed.ballista} {-2shrinking.potion} {+2vial}
    [You q[Q]uaff your shrinking potions[.]] and board your
    paper airliner. As one, you pull the pin and slingshot
    into the sky toward home!
    <- # THE END
  + <get pumpkin> {not pumpkin} {+pumpkin} {-2hand}
    [You g[G]et a pumpkin[.]], grasping it with two hands.
  + <get flower> {not flower} {+flower} {-hand}
    [You p[P]luck a blue flower. ]
    ->grow.airplane.clue()
  ->common()
  + <go jungle> <south> {homestead}
    [You f[F]ollow the path into the jungle. ]
    //->jungle
  + <go beach> <south east> {bridge}
    [You c[C]ut accross the bridge down to the beach. ]
    //->beach
  ->maybe.chat()
  >
  ->hills.choice

- @homestead.formula()
  - {at==hills and homestead and flower and freshwater.pumpkin}
    {"Well, we already have a giant pumpkin house.  We don'}t have much
    use for another,"} says the {~boy|girl}.
  + <grow homestead> {at==hills and not homestead}
    {-flower} {-freshwater.pumpkin} {+3hand} {+homestead}
    [You f[F]erment the blue flower in the pumpkin water. ]
    The pumpkin grows as big as a house.
    You may need to find a way to make a growing potion without a pumpkin.
    // The two of you establish a base around
    the house, planting a garden and laying a path that leads to the southerly
    jungle.
    + <> <south> <continue> [Continue... ]
    >
    ->jungle

- @jungle
  ! at = jungle
  Jungle.
  - {not tap} There are rubber trees and stands of bamboo.
  - {tap} There are stands of bamboo and sap flows from a rubber tree.
  Little red mushrooms litter the forest floor.
  - {not bridge} A river runs to the sea along the eastern margin
    of the jungle.
  - {bridge} The path through the jungle runs from the hills to the
    north and across a bridge to the east.
  - {ballista} Your giant slingshot is getting heavy, but neither of you can see a
    clearing large enough to put it down {~nearby|near here}.
  @jungle.choice
  + <get all bamboo> {not bamboo} {not bridge}
    [You f[F]ill your {(hand<4)?remaining} arms with bamboo. ]
    {+hand bamboo} {=0 hand}
  + <get bamboo> {not bamboo} {bridge} {+bamboo} {-hand}
    [You c[C]ut some bamboo. ]
    ->bamboo.choice
  + <get mushroom> {not mushroom} {+mushroom} {-hand}
    [You p[P]ick a mushroom. ]
  ->common()
  + <go hills> <north> [You r[R]eturn to your homestead in the hills. ]
    //->hills
  + <go river> <east> {not bridge} [You w[W]alk through the jungle to a river. ]
    ->river
  + <go beach> <east> {bridge} [You c[C]ross the bridge over the river[ to the beach.]]
    and head down to the beach.
    //->beach
  ->maybe.chat()
  >
  ->jungle.choice

- @tap.formula()
  + <tap rubber tree>
    {at==jungle} {rock or hammer} {not tap} {-bamboo} {+hand} {+tap}
    [You t[T]ap a shoot of bamboo into a rubber tree with the
    {(hammer)?hammer|rock}. ]
    Sap begins to flow from the tap.
    ->jungle.choice

- @bridge.formula()
  + <build bridge>
    {at==jungle} {-3bamboo} {+bridge} {+3hand}
    [You b[B]uild a bridge from three bundles of bamboo[.]],
    crossing over and walking down to the beach.
    //->beach

- @river
  - {bamboo>0 and bamboo<3}
    You might make a safe bridge with more bamboo.
  ->bridge.formula()
  + <fill pumpkin with fresh water>
    {-pumpkin} {+freshwater.pumpkin}
    [You f[F]ill your pumpkin with fresh water from the river. ]
  + <fill vial with fresh water>
    {-vial} {+freshwater.vial}
    [You f[F]ill {(vial)|the|a} vial with fresh water from the river. ]
  + <go beach> <east> {bridge} [You c[C]ross the bridge to the beach. ]
    ->beach
  + <> [You r[R]eturn to the jungle. ]
    ->jungle
  ->maybe.chat()
  >
  ->return

- @beach
  ! at = beach
  Beach. There is a marsh of reeds and a sandy beach.
  - {ballista} Your giant slingshot is getting heavy, but this doesn'}t look
    like a good place to keep it. It'}ll get stuck in the sand, and
    it would be a burden to carry up the mountain.
  @beach.choice
  + <get reed> {not reed} {+reed} {-hand}
    [You c[C]ut a reed from the marsh grasses. ]
    ->reed.choice
  ->common()
  + <go jungle> <west>
    [You c[C]ross the bridge and return to the jungle. ]
    //->jungle
  + <go hills> <north west>
    [You c[C]ross the bridge and cut back to your homestead in the hills. ]
    //->hills
  + <go mountain> <north>
    {lion and not ballista}
    [You w[W]alk north across an isthmus to the foot of a volcano. ]
    //->mountain.gate
  + <go mountain> <north>
    {not lion and not ballista}
    [You m[M]ake your way over the isthmus to the volcano'}s
    {~summit|peak|caldera}. ]
    //->mountain
  ->maybe.chat()
  >
  ->beach.choice

- @mountain.gate
  ! at = mountain
  Foot of the mountain.
  You stand on a path that leads up the slopes of an active volcano.
  A lion guards the way.
  - {not mushroom}
    It looks hungry.
  - {mushroom}
    The lion roars. You must be carrying something delicious.
  @mountain.gate.choice
  + <give lion mushroom> []
    You offer the mushroom to the lion.
    ->give.lion.mushroom
  ->common()
  + <go beach> <south> <down>
    [You f[F]lee back to the beach. ]
    //->beach
  >

- @mountain
  ! at = mountain
  Mountain.
  You stand at the peak of an active volcano.
  Liquid hot lava bubbles in the cinder cone, flowing from the far end down to
  the sea.
  The stench of burning sulfer envelopes you.
  - {sand.pumpkin and not reed} Perhaps you could blow glass from molten sand
    if you found something to blow through.
  @mountain.choice
  + <get rock>
    {+rock} {-hand}
    [You p[P]ick up a volcanic rock[.]],
    holding it with one hand.
  ->common()
  + <go beach> <south> <down>
    [You d[D]escend to the beach. ]
    //->beach
  ->maybe.chat()
  >
  ->mountain.choice

- @common()
  - {rock and paper and not roshambo}
    {"Well, we'}ve got rock and paper: all we need are scissors!"} jokes the
    {~boy|girl}. {+roshambo}
  - {not hand and growing.potion}
    You might need a free hand if you wish to grow something with growing
    potion.
  ->grow.airplane.clue()
  ->inventory()
  ->maybe.drop()

- @inventory()
  + <pumpkin> {pumpkin}
    [You have a pumpkin. ]
    ->pumpkin
  + <freshwater pumpkin> {freshwater.pumpkin}
    [You have a pumpkin full of fresh water. ]
    ->freshwater.pumpkin
  + <brine pumpkin> {brine.pumpkin}
    [You have a pumpkin full of briny sea water. ]
    ->brine.pumpkin
  + <sap pumpkin> {sap.pumpkin}
    [You have a pumpkin full of rubber sap. ]
    ->sap.pumpkin
  + <sand pumpkin> {sand.pumpkin}
    [You have a pumpkin full of sand. ]
    ->sand.pumpkin
  + <flower> {flower}
    [You have {(flower)||a|->number(flower)} blue flower{(flower)|s||s}. ]
    ->flower
  + <bamboo> {bamboo}
    [You have bamboo. ]
    ->bamboo
  + <mushroom> {mushroom}
    [You have {(mushroom)||a|->number(mushroom)}
    mushroom{(mushroom)|s||s}. ]
    ->mushroom
  + <reed> {reed}
    [You have some reeds. ]
    ->reed
  + <soaked reed> {soaked.reed}
    [You have some soaked reeds. ]
    ->soaked.reed
  + <rock> {rock}
    [You have {(rock)||a|->number(rock)} rock{(rock)|s||s}. ]
    ->rock
  + <rubber> {rubber}
    [You have a mass of rubber. ]
    ->rubber
  + <brine vial> {brine.vial}
    [You have {(brine.vial)||a|->number(brine.vial)} vial{(brine.vial)|s||s} of
    brine. ]
    ->brine.vial
  + <freshwater vial> {freshwater.vial}
    [You have {(freshwater.vial)||a|->number(freshwater.vial)}
    vial{(freshwater.vial)|s||s} of fresh water. ] ->freshwater.vial
  + <vial> {vial}
    [You have
      {(brine.vial or freshwater.vial)?
      | {(vial)||an empty|a pair of empty|->number(vial) empty}
      | {(vial)||a|a pair of|->number(vial)}
      }
      glass vial{(vial)|s||s}.
    ]
    ->vial
  + <shrinking potion> {shrinking.potion}
    [You have {(shrinking.potion)||a|a pair of|->number(vial)}
    vial{(shrinking.potion)|s||s} of shrinking potion. ]
    ->shrinking.potion
  + <growing potion> {growing.potion}
    [You have {(growing.potion)||a|a pair of|->number(vial)}
    vial{(growing.potion)|s||s} of growing potion. ]
    ->growing.potion
  + <ballista> {ballista}
    [You have a giant slingshot. ]
    ->ballista
  + <hammer> {hammer} [You have a hammer. ]
    ->hammer
  + <paper> {paper} [You have some paper. ]
    ->paper
  + <airplane> {airplane} [You have a paper airplane. ]
    ->airplane
  + <giant airplane> {giant.airplane} [You have a giant paper airplane. ]
    ->giant.airplane

- @maybe.drop()
  + {not hand}
    [All of your hands are full between you. ]
    ->drops

- @drops
  Perhaps you can drop something to free up some hands.
  ->inventory()
  + <> [You k[K]eep everything. ]
  >
  ->return

- @pumpkin
  You hold the pumpkin with two hands.
  @pumpkin.choice
  # TODO tiny pumpkin formula (from vial)
  # TODO giant pumpkin formula (from vial)
  ->brine.vial.pumpkin.formula()
  + <fill pumpkin with fresh water>
    {at==hills} {-pumpkin} {+freshwater.pumpkin}
    [You f[F]ill the pumpkin with water from the river. ]
    ->freshwater.pumpkin.choice
  + <fill pumpkin with sap>
    {at==jungle} {tap} {-pumpkin} {+sap.pumpkin}
    [You f[F]ill the pumpkin with sap from the tapped rubber tree. ]
    ->sap.pumpkin.choice
  + <fill pumpkin with sand> {at==beach} {-pumpkin} {+sand.pumpkin}
    [You f[F]ill the pumpkin with white beach sand. ]
    ->sand.pumpkin.choice
  + <fill pumpkin with brine> {at==beach} {-pumpkin} {+brine.pumpkin}
    [You f[F]ill the pumpkin with briny water from the sea. ]
    ->brine.pumpkin.choice
  + <drop pumpkin> {-pumpkin} {+2hand}
    [You d[D]rop the pumpkin[.]], smashing it to pulpy bits.
    ->return
  + <> <keep> [You k[K]eep the pumpkin. ] ->return
  >

- @freshwater.pumpkin
  You hold a pumpkin full of fresh water with two hands.
  @freshwater.pumpkin.choice
  ->homestead.formula()
  ->soaked.reed.formula()
  + <drop freshwater pumpkin>
    {-freshwater.pumpkin} {+2hand} [You d[D]rop the pumpkin. ]
    The pumpkin goes to pieces and the water flows away.
    ->return
  + <spill freshwater pumpkin>
    {-freshwater.pumpkin} {+pumpkin} [You s[S]pill the water
    from the pumpkin. ]
    ->pumpkin.choice
  + <> <keep> [You k[K]eep the pumpkin. ] ->return
  >

- @brine.pumpkin
  You hold a pumpkin full of fresh water with two hands.
  @brine.pumpkin.choice
  # TODO tiny pumpkin formula (with mushroom)
  ->brine.vial.pumpkin.formula()
  ->soaked.reed.formula()
  + <drop brine pumpkin>
    {-brine.pumpkin} {+2hand} [You d[D]rop the pumpkin. ]
    The pumpkin goes to pieces and the water flows away.
    ->return
  + <spill brine pumpkin>
    {-brine.pumpkin} {+pumpkin} [You s[S]pill the water
    from the pumpkin. ]
    ->pumpkin.choice
  + <> <keep> [You k[K]eep the pumpkin. ] ->return
  >

- @sap.pumpkin
  You hold a pumpkin full of rubber sap with two hands.
  @sap.pumpkin.choice
  + <cook rubber sap>
    {at==mountain} {-sap.pumpkin} {+rubber} {+hand}
    [You c[C]ook the rubber sap on the brimstone. ]
    In time, the pumpkin burns away revealing a mass of vulcanized rubber.
    ->mountain.choice
  + <drop pumpkin full of sap> {-sap.pumpkin} {+2hand} [You d[D]rop the pumpkin[.]],
    losing the rubber.
    ->return
  + <spill sap from pumpkin> {-sap.pumpkin} {+pumpkin}
    [You s[S]pill the rubber sap.]
    ->return
  + <> <keep> [You k[K]eep the pumpkin. ] ->return
  >

- @sand.pumpkin
  You hold a pumpkin full of beach sand with two hands.
  - {at==mountain} {sand.pumpkin}
    - {not reed}
      You might be able to blow glass from beach sand if you found a tube to
      blow through.
    - {reed}
      You could blow glass with your reed and bucket of beach sand.
  @sand.pumpkin.choice
  ->vial.formula()
  + <drop sand pumpkin>
    {-sand.pumpkin} {+2hand} [You d[D]rop the pumpkin. ]
    It goes to pieces and the sand spills.
    ->return
  + <spill sand pumpkin>
    {-sand.pumpkin} {+pumpkin}
    [You s[S]pill the sand from the pumpkin. ]
    ->pumpkin.choice
  + <> <keep> [You k[K]eep the pumpkin. ] ->return
  >

- @flower
  You hold the blue flower{(flower)|s||s} gently with ->number(flower)
  hand{(flower)|s||s}.
  @flower.choice
  # TODO giant flower formula (with growing potion)
  # TODO tiny flower formula (with shrinking potion)
  ->homestead.formula()
  ->growing.potion.formula()
  + <drop flower>
    {-flower} {+hand}
    [You t[T]oss {(flower)|the|a} blue flower {~away|aside}. ]
    - {at == mountain}
      The flower withers to dust in the fiery chasm.
    {(flower)?->flower}
    ->return
  + <get flower>
    {at==hills} {+flower} {-hand}
    [You p[P]luck another blue flower. ] ->flower
  + <> <keep> [You k[K]eep the blue flower{(flower)|s||s}. ] ->return
  >

- @bamboo
  You hold bundle{(bamboo)|s||s} of bamboo with ->number(bamboo)
  hand{(bamboo)|s||s}.
  @bamboo.choice
  # TODO giant bamboo formula
  # TODO reeds from bamboo formula with shrinking potion
  ->bridge.formula()
  ->ballista.formula()
  ->tap.formula()
  ->hammer.formula()
  + <drop bamboo>
    {-bamboo} {+hand}
    [You d[D]rop {(bamboo)|the|a} bundle of bamboo. ]
    {(bamboo)?->bamboo}
    ->return
  + <get bamboo>
    {at==jungle} {+bamboo} {-hand}
    [You c[C]ut another stalk of bamboo. ] ->bamboo
  + <> <keep> [You k[K]eep the bamboo. ] ->return
  >

- @mushroom
  You hold the little red mushroom{(mushroom)|s||s} with
  {(mushroom)||->number(mushroom)} hand{(mushroom)|s||s}.
  @mushroom.choice
  # TODO giant mushroom formula
  # TODO tiny mushroom formula
  ->shrinking.potion.formula()
  ->growing.potion.formula()
  + <give lion mushroom>
    {at==mountain} {-lion} {+cat} {-mushroom} {+hand}
    [You o[O]ffer the lion a mushroom. ]
    @give.lion.mushroom
    The lion greedily accepts and shrinks to a diminuitive size, becoming a
    gentle cat, opening the way to the volcano.
    + <go mountain>
      [You t[T]ravel across the isthmus up the vulcan terraces. ]
      //->mountain
    + <go beach>
      [You r[R]eturn to the beach. ]
      //->beach
    >
  + <eat mushroom>
    [Eat {(mushroom)||the|a} mushroom.]
    The {~boy|girl} tastes the mushroom and exclaims, {"Oh, this
    is surely edible, but far too bitter to eat raw."}
    - {lion} Maybe this island has animals that would find it palatable.
    + <> <continue> [ Continue. ]
    >
    ->return
  + <get mushroom>
    {at==jungle} {+mushroom} {-hand}
    [You p[P]ick another mushroom. ]
  + <drop mushroom>
    {-mushroom} {+hand}
    [You t[T]oss {(mushroom)|the|a} red mushroom {~away|aside}. ]
    {(not mushroom)?->return}
  + <> <keep> [You k[K]eep the mushroom{(mushroom)|s||s}. ]
    ->return
  >
  ->mushroom.choice

- @reed
  You have {(reed)||a few reeds|bundles of reeds} held in ->number(reed)
  hand{(reed)|s||s}.
  - {not launch.pad and not airplane and not paper and not soaked.reed}
    These reeds might be useful for making paper.
    They would need to be made plyable somehow.
  @reed.choice
  ->vial.formula()
  ->soaked.reed.formula()
  # TODO giant reed formula (bamboo) with growing potion
  # TODO tiny reed formula (straw) with shrinking potion
  + <drop reed>
    {-reed} {+hand}
    [You d[D]rop {(reed)|the remaining|some} reeds. ]
    {(not reed)?->return}
    + <drop reed>
      [You d[D]rop the remaining reeds, freeing up ->number(reed)
      hand{(reed)|s||s}. ]
      {+reed hand} {=0 reed}
    + <> <keep> [You k[K]eep the reeds. ]
    >
    ->return
  + <get reed>
    {at==beach} {+reed} {-hand}
    [You c[C]ut more reeds from the marsh. ]
  + <> <keep> [You k[K]eep the reeds. ]
    ->return
  >
  ->reed

- @soaked.reed.formula()
  + <soak reeds in pumpkin>
    {brine.pumpkin or freshwater.pumpkin} {-reed} {+soaked.reed}
    [You s[S]oak reeds in the pumpkin. ]
    Some time later, you notice that they have become rather plyable.  Maybe
    you can wrap, weave, tie, or mash these reeds.
    ->return

- @soaked.reed
  You have {(soaked.reed)||a few soaked reeds|a bundle of soaked reeds} held in
  ->number(soaked.reed) hand{(soaked.reed)|s||s}.
  - {not launch.pad and not airplane and not paper}
    You might be able to mash these reeds into pulp.
    - {rock and not hammer}
      Sadly, this rock alone would make hard work of paper making.
      Perhaps you can use it to make a hammer.
  @soaked.reed.choice
  ->hammer.formula()
  ->paper.formula()
  + <drop soaked reed>
    {-soaked.reed} {+hand}
    [You d[D]iscard {(reed)|the|some} reeds. ]
    {(not soaked.reed)?->return}
  + <> <keep> [You k[K]eep the reeds. ]
    ->return
  >
  ->soaked.reed

- @paper.formula()
  + <mash reed>
    {hammer} {-soaked.reed} {+paper}
    [You m[M]ash reeds with hammer[.]], creating a mushy pulp
    from the reeds'} pith. You leave these to dry and
    they become coarse paper.
    ->paper.choice

- @paper
  You have {(paper)||a|->number(paper)} crisp sheets of paper, held in
  ->number(paper) hand{(paper)|s||s}.
  - {airplane} You already have a paper airplane and you agree that you can
    both fly in one if you shrink yourselves.
  @paper.choice
  # TODO giant sheet of paper formula
  # TODO giant sheet of paper folded to giant airplane formula
  # TODO tiny sheet of note paper formula
  + <fold paper> <make airplane>
    {not airplane} {-paper} {+airplane}
    [You f[F]old a sheet of paper into an airplane. ]
    ->airplane.choice
  + <drop paper> {-paper} {+hand}
    [You d[D]rop the paper. ]
    ->airplane.choice
  + <> <keep> [You k[K]eep the paper. ]
    ->return
  >
  ->paper

- @grow.airplane.clue()
  - {flower and airplane and not giant.airplane and not launch.pad}
    {"Clearly this airplane is not going to eat the flower.
    We will have to splash a growing potion on it or it will never
    carry us both,"} says the {~boy|girl}.

- @grow.airplane.formula()
  + <grow airplane>
    {-airplane} {+giant.airplane} {-growing.potion} {+vial} {-hand}
    [You s[S]plash the growing potion on your little paper airplane[.]]
    and it grows into a fabulous paper jet liner for two very small people.
    One of you has to hold it with both hands to keep it from catching the
    wind. The other keeps the now empty vial.
    ->giant.airplane.choice

- @airplane
  You have a paper airplane.
  It isn'}t quite big enough to carry the boy and girl, even if they were tiny.
  @airplane.choice
  + <drop airplane> <throw airplane>
    {-airplane} {+hand}
    [You t[T]hrow the airplane. ]
    It catches the wind and flies away into the sea.
  + <> <keep> [You k[K]eep the airplane. ]
  >
  ->return

- @giant.airplane
  You have a giant paper airplane.
  You prevent the wind from carrying it off with all of two hands.
  - {at<>hills} You need to find somewhere to put this down.
  @giant.airplane.choice
  + <put giant airplane on ballista>
    {at==hills} {-placed.ballista} {+launch.pad} {-giant.airplane} {+2hand}
    [You p[P]lace the plane on the launch platform. ]
  + <drop giant airplane> <throw airplane>
    {-giant.airplane} {+2hand}
    [You l[L]et the wind take the plane. ]
  + <> <keep> [You h[H]old the plane firmly. ]
  >
  ->return

- @hammer.formula()
  + <make hammer>
    {not hammer} {-rock} {-soaked.reed} {-bamboo} {+hammer} {+2hand}
    [You b[B]ind the rock to the bamboo with the reed[.]],
    constructing a sturdy hammer.
    Perhaps you can use this to mash things to pulp.
    ->return

- @hammer
  You have a hammer.
  - {not airplane and not paper and not soaked.reed}
    A hammer might be better than a rock for making pulp for paper.
    You'}ll after all need to make a paper airplane to fly away from this
    island.
  @hammer.choice
  ->paper.formula()
  # TODO giant hammer formula
  # TODO tiny hammer formula
  + <drop hammer> {-hammer} {+hand}
    [You d[D]rop the hammer[.]], (in the quite literal sense) losing it
    {(at)
    | in the grass.
    | in a thicket of bamboo.
    | in the sea.
    | in the caldera.
    }
     ->return
  + [You k[K]eep the hammer. ] ->return
  >
  ->hammer

- @rock
  You have {(rock)||a rock|load of stones} held in ->number(rock)
  hand{(rock)|s||s}.
  - {soaked.reed and not bamboo and not hammer}
    You might be able to use these soaked reeds to bind the rock
    to something and make a hammer.
  ->tap.formula()
  ->hammer.formula()
  # TODO giant rock formula
  # TODO tiny rock formula (sand)
  + <drop rock>
    {-rock} {+hand}
    [You d[D]rop {(rock)|the|a} rock. ]
    {(not rock)?->return}
  + <> <keep> [You k[K]eep the rock{(rock)|s||s}. ]
    ->return
  >
  ->rock

- @rubber
  You have a mass of rubber held in ->number(rubber) hand{(rubber)|s||s}.
  ->ballista.formula()
  + <drop rubber> [You d[D]rop the mass of rubber[.]],
    freeing ->number(rubber) hand{(rubber)|s||s}.
    {+rubber hand} {=0 rubber}
    ->return
  + <> <keep> [You k[K]eep the rubber. ]
    ->return
  >

- @ballista.formula()
  + <make ballista>
    {not ballista} {-bamboo} {-rubber} {+ballista}
    [You a[A]ssemble a ballista with the rubber and a forked pair of
    bamboo shoots. ]
    - {shrinking.potion} ->ballista
    ->return

- @ballista
  You have a ballista held in two hands.
  + <throw rock with ballista> {-rock} {+hand}
    [You t[T]hrow a rock with the ballista[.]],
    sending it sailing far out to sea.
    You don'}t even see it splash.
    {"It seems a waste of a stone,"} the {~boy|girl}
    says, {"but it freed a hand."}
  + <break ballista> {-ballista} {+bamboo} {+rubber}
    [You b[B]reak down the ballista. ]
  + <drop ballista> {-ballista} {+2hand}
    [You d[D]estroy the ballista. ]
  + <put ballista>
    {at==hills} {-ballista} {+placed.ballista} {+2hand}
    [You p[P]ut your ballista down. ]
    You aim it toward home.
    - {not airplane and not giant.airplane}
      Now you need something to launch.
    - {airplane} You could send an airplane really far with this,
      if the airplane were big enough for it.
    ->hills.choice
  + <> <keep> [You k[K]eep the ballista. ]
  >
  ->return

- @vial.formula()
  + <make vials> <blow glass>
    {at==mountain} {reed or bamboo} {-sand.pumpkin} {+2vial}
    [You b[B]low a pair of of glass vials from molten sand in the cinder cone. ]
    ->mountain.choice

- @vial
  You have {(vial)||a vial|->number(vial) vials} held in ->number(vial)
  hand{(vial)|s||s}.
  @vial.choice
  + <drop vial> {-vial} {+hand}
    [You d[D]rop {(vial)|the|a} glass vial[.]]
    and it shatters into sharp bits.
    - {vial} You have ->number(vial) remaining vial{(vial)|s||s}.
      ->vial.choice
    ->return
  + <fill vial with brine>
    {at==beach} {-vial} {+brine.vial}
    [You f[F]ill {(vial)|the|a} vial with brine from the sea. ]
    ->brine.vial.choice
  + <fill vial with freshwater>
    {at==hills} {-vial} {+freshwater.vial}
    [You f[F]ill {(vial)|the|a} vial with fresh water from the river.]
    ->freshwater.vial.choice
  + <> <keep> [You k[K]eep the vial{(vial)|s||s}. ] ->return
  >
  ->vial

- @brine.vial.pumpkin.formula()
  + <fill vial with brine from pumpkin>
    {brine.pumpkin} {-vial} {+brine.vial}
    [You f[F]ill a vial with brine from the pumpkin. ]
    ->brine.vial.choice

- @brine.vial
  You have {(brine.vial)||a vial|->number(brine.vial) vials} of brine held in
  ->number(brine.vial) hand{(brine.vial)|s||s}.
  @brine.vial.choice
  ->shrinking.potion.formula()
  ->growing.potion.formula()
  + <spill brine from vial>
    {-brine.vial} {+vial}
    [You s[S]pill the brine from the vial. ]
    ->vial.choice
  + <> <keep> [You k[K]eep the brine vial{(brine.vial)|s||s}. ]
    ->return
  >
  ->brine.vial.choice

- @shrinking.potion.formula()
  - {mushroom and freshwater.vial and not brine.vial}
    You might be able to make a shrinking potion with this mushroom,
    since it did quite well on the lion, but perhaps sweet water
    won'}t ferment a mushroom like it does the blue flowers.
  + <make shrinking potion>
    {-mushroom} {-brine.vial} {+shrinking.potion} {+hand}
    [You f[F]erment a mushroom in the vial of brine. ]
    ->shrinking.potion.choice

- @shrinking.potion
  You have {(shrinking.potion)||a vial|->number(shrinking.potion) vials}
  of shrinking potion.
  @shrinking.potion.choice
  # TODO shrink lion back to cat
  + <spill shrinking potion> {-shrinking.potion} {+vial}
    [You s[S]pill the shrinking potion from the vial. ]
    ->vial.choice
  + <> <keep> [You r[R]etain the vial of shrinking potion. ]
    ->return
  >

- @freshwater.vial.pumpkin.formula()
  + <fill vial with freshwater from pumpkin>
    {freshwater.pumpkin} {-vial} {+freshwater.vial}
    [You f[F]ill a vial with fresh water from the pumpkin. ]
    ->freshwater.vial.choice

- @freshwater.vial
  You have {(freshwater.vial)||a vial|->number(freshwater.vial) vials} of fresh
  water held in ->number(freshwater.vial) hand{(freshwater.vial)|s||s}.
  @freshwater.vial.choice
  ->shrinking.potion.formula()
  ->growing.potion.formula()
  + <spill freshwater from vial> {-freshwater.vial} {+vial}
    [You s[S]pill the water from the vial. ]
    ->vial.choice
  + <> <keep> [You k[K]eep the fresh water vial{(freshwater.vial)|s||s}. ]
    ->return
  >
  ->freshwater.vial.choice

- @growing.potion.formula()
  - {flower and brine.vial and not freshwater.vial}
    You recall that you grew your homestead with a pumpkin filled with fresh
    water. This vial of brine might not be the key to a growing potion.
  + <make growing potion>
    {-flower} {-freshwater.vial} {+growing.potion} {+hand}
    [You f[F]erment a flower in the vial of brine. ]
    The water turns into a blue potion for growing.
    ->growing.potion.choice

- @growing.potion
  You have {(growing.potion)||a vial|->number(growing.potion) vials}
  of growing potion.
  @growing.potion.choice
  # TODO grow cat to lion
  ->grow.airplane.formula()
  + <spill growing potion>
    {-growing.potion} {+vial}
    [You s[S]pill the growing potion from the vial. ]
    ->vial.choice
  + <> <keep> [You r[R]etain the vial of growing potion. ]
    ->return
  >

- @return
  //
  {(at)
  | ->hills
  | ->jungle
  | ->beach
  | ->mountain
  }

- @maybe.chat()
  + <chat> [{~Talk|Discuss|Chat|Take a break|Rest}. ]
    ->chat() ->return

- @chat()
  {@(chat)|
    The {~boy|girl} cries, {"I sorely wish we had not lost our potions in the crash."}
  | The {~boy{=0reply}|girl{=1reply}} asks, {"Do you wonder if we will ever get home?"}
    The {(reply)|girl|boy} replies, {"Oh, I do not wonder at all. We will surely."}
  | {"This island is lovely,"} you both agree, {"but it is not home, and Mom and Dad will
    surely miss us if we don'}t return before they come back with the groceries."}
  | {"I don'}t suppose you brought a book?"} the {~boy|girl} asks.
  | {"I could stay here forever,"} says the {~boy|girl}, {"but we have school
    tomorrow."}
  | {"I can'}t wait to get home to our workshop. I'}m sure this would never have
    happened with our styrofoam-hulled airplane. We need to finish the motor,"}
    says the {~boy|girl}.
  | {"We have never made paper for our planes before,"} says the {~boy,
    and the girl replies, |girl, and the boy replies, }
    {"Indeed. This is quite an adventure.
    {(not giant.airplane and not launch.pad)?
      We will have to
      {(airplane)? make a little paper and}
      grow the airplane."}
    }
  | {"I am not sure whether I like cats or dogs better,"} says the
    {~boy|girl}, {"but I would sure love for us to take one
    on our adventures."}
  } {+chat}
  + <more> [{"Let'}s {~chat|rest|talk} more."} ] ->chat
  + <> <back> [{"Let'}s get back to work."} ] <-
  >

- @number(number)
  {(number)|no|one|two|three|four}
